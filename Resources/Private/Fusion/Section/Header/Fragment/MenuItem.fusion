prototype(Litefyr.Presentation:Section.Header.Fragment.MenuItem) < prototype(Neos.Fusion:Component) {
    @propTypes {
        uri = ${PropTypes.string.isRequired}
        label = ${PropTypes.string.isRequired}
        isLightbox = ${PropTypes.boolean}
        fontSize = ${PropTypes.string}
    }

    subItems = null

    @if.hasLabelAndUri = ${this.uri && this.label}
    defaultLinkClass = 'leading-tight transition-colors'

    renderer = afx`
        <li class="group relative h-full">
            <a
                href={props.lightboxIframeSrc || props.uri}
                class={[props.defaultLinkClass, props.fontSize, 'whitespace-nowrap h-full p-4 flex flex-col items-center justify-center text-center text-theme-header group-hocus-within:text-theme-accent transparent-header:group-hocus-within:text-theme-header']}
                :class="$isActivePage()?'font-bold':'font-normal'"
                :aria-current="$isCurrentPage()&&'page'"
                data-pswp-type={props.isLightbox ? "fetch" : null}
                data-pswp-src={props.lightboxIframeSrc ? props.uri : null}
            >
                {props.label}
            </a>
            <ul
                @if={Carbon.Array.check(props.subItems)}
                class={["pointer-events-none absolute bg-theme-back -bg-lightness-offset-5 shadow-dropdown w-max max-w-sm font-normal rounded-box overflow-clip opacity-0 -translate-y-4 transition duration-300 group-hocus-within:z-10 group-hocus-within:opacity-100 group-hocus-within:translate-y-0 group-hocus-within:pointer-events-auto group-hocus-within:block", iterator.isLast ? "right-0" : "left-0"]}
            >
                <Neos.Fusion:Loop items={props.subItems}>
                    <li>
                        <a
                            href={item.lightboxIframeSrc || item.uri}
                            class={[props.defaultLinkClass, "block py-2 px-4 overflow-clip text-theme-front hocus:bg-theme-accent hocus:text-theme-back"]}
                            :class="$isActivePage()?'font-bold':'font-normal'"
                            :aria-current="$isCurrentPage()&&'page'"
                            data-pswp-type={item.isLightbox ? "fetch" : null}
                            data-pswp-src={item.lightboxIframeSrc ? item.uri : null}
                        >
                            {item.label}
                        </a>
                    </li>
                </Neos.Fusion:Loop>
            </ul>
        </li>
    `
}
