prototype(Litespeed.Presentation:Section.Header.Fragment.MobileMenuItem) < prototype(Neos.Fusion:Component) {
    @propTypes {
        uri = ${PropTypes.string.isRequired}
        label = ${PropTypes.string.isRequired}
        uriPathSegment = ${PropTypes.string}
        content = ${PropTypes.string}
    }

    uri = ${null}
    label = ${null}
    uriPathSegment = ${Array.last(String.split(this.uri, '/'))}
    content = ${null}
    subItems = ${null}
    menuLevel = 1
    collapse = ${Configuration.setting('Litespeed.Presentation.menu.mobile.collapseSubItems')}

    @if.hasLabelAndHref = ${this.uri && this.label}

    defaultClass = Neos.Fusion:DataStructure {
        base = 'block leading-tight underline-offset-8 decoration-theme-accent text-theme-front hocus:text-theme-accent focus:outline-none focus-visible:underline'
        1 = ${this.base + ' text-2xl'}
        2 = ${this.base + ' text-base py-1'}
        3 = ${this.base + ' text-sm'}
    }

    collapseIcon = ${Configuration.setting('Litespeed.Presentation.icons.accordion')}

    renderer = afx`
        <li x-data={!!(props.collapse && props.subItems)} x-disclosure={!!(props.collapse && props.subItems)} class={Tailwind.merge(props.subItems ? "sm:col-span-2" : null, props.subItems && props.menuLevel > 1 ? "mb-4" : null)}>
            <Carbon.Eel:Tag tagName={props.collapse && props.subItems && 'span'} attributes.class="inline-flex gap-2 items-center justify-between min-w-[30%]">
                <a
                    href={props.uri}
                    class={props.defaultClass[props.menuLevel]}
                    :class="$isActivePage()&&'font-bold'"
                    :aria-current="$isCurrentPage()&&'page'"
                >
                    {props.label}
                </a>
                <button
                    @if={!!(props.collapse && props.subItems)}
                    x-disclosure:button
                    type="button"
                    class="flex items-center rounded-full size-12 -my-8 justify-center transform transition-all ring-0 ring-theme-accent duration-200 text-theme-front hocus:text-theme-accent focus:outline-none focus-visible:ring-4"
                >
                    <Litespeed.Theme:Component.Icon
                        styleAndIcon={props.collapseIcon.always}
                        attributes.class="size-6 transition duration-500"
                        attributes.:class="$disclosure.isOpen&&'rotate-180'"
                    />
                    <Litespeed.Theme:Component.Icon
                        @if={!props.collapseIcon.always}
                        styleAndIcon={props.collapseIcon.close}
                        attributes.class="size-6 transition duration-500"
                        attributes.x-show="$disclosure.isOpen"
                        attributes.x-cloak={true}
                    />
                    <Litespeed.Theme:Component.Icon
                        @if={!props.collapseIcon.always}
                        styleAndIcon={props.collapseIcon.open}
                        attributes.class="size-6 transition duration-500"
                        attributes.x-show="!$disclosure.isOpen"
                    />
                </button>
            </Carbon.Eel:Tag>
            <ul @if={props.subItems} x-disclosure:panel={props.collapse} x-collapse={props.collapse} class="grid grid-flow-row grid-cols-1 sm:grid-cols-2 gap-x-4">
                <Neos.Fusion:Loop items={props.subItems}>
                    <Litespeed.Presentation:Section.Header.Fragment.MobileMenuItem {...item} menuLevel={props.menuLevel + 1} />
                </Neos.Fusion:Loop>
            </ul>
        </li>
    `
}
