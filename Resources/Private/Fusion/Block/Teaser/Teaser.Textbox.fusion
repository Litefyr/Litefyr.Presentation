prototype(Litefyr.Presentation:Block.Teaser.Textbox) < prototype(Neos.Fusion:Component) {
    @propTypes {
        headline = ${PropTypes.string}
        text = ${PropTypes.string}
        href = ${PropTypes.string}
        date = ${PropTypes.any}
        time = ${PropTypes.string}
        label = ${PropTypes.string}
        alternativeText = ${PropTypes.string}
        bottomContent = ${PropTypes.string}
        placeholder = ${PropTypes.boolean}
        class = ${PropTypes.anyOf( PropTypes.string, PropTypes.arrayOf( PropTypes.string ) )}
    }
    @styleguide {
        props {
            headline = Sitegeist.ChitChat:Line {
                length = 20
            }
            text = Sitegeist.ChitChat:Line {
                length = 200
            }
            href = '#'
            time = '<span>4 min zum lesen</span>'
            alternativeText = 'Bildbeschreibung'
            placeholder = false
            date = ${Date.now()}
            imageSource = Carbon.Image:Helper.ImageSource {
                outputDummy = true
            }
        }
    }

    @if.hasHrefAndContent = ${this.placeholder || this.href}

    imageWith = 937
    ratio = 1.875

    theme = 'default'

    rootClass = null

    @private.rootClass = '-teaser flex-1 flex flex-col @5xl:flex-row shadow-teaser-textbox overflow-clip rounded-box group/teaser hover:z-10 focus:z-10'

    renderer = afx`
        <div class={Tailwind.merge("animation-elements w-full flex flex-col @container", props.class)}>
            <div
                @if={props.placeholder}
                aria-hidden="true"
                class={Tailwind.merge(private.rootClass, "aspect-[15/8] flex items-center justify-center text-theme-front bg-theme-back bg-lightness-offset-5 animate-pulse")}
                data-theme={props.theme}
            >
                <Litefyr.Presentation:Atom.Icon
                    styleAndIcon={Configuration.setting('Litefyr.Presentation.icons.loadingIndicator')}
                    attributes.class="size-5 animate-spin"
                />
            </div>
            <a
                @if={!props.placeholder}
                href={props.href}
                class={Tailwind.merge(private.rootClass, "aspect-[15/8] relative transition duration-500 hocus-within:scale-105 hocus-within:shadow-teaser-textbox-hover", props.rootClass)}
                data-theme={props.theme}
            >
                <Litefyr.Presentation:Block.ImageWithTextOverlay
                    imageSource={props.imageSource}
                    image={props.image}
                    alternativeText={props.alternativeText}
                    width={props.imageWith}
                    height={Math.round(props.imageWith / props.ratio)}
                    text={props.headline}
                    boxDirection="inline"
                    insideLink={true}
                    enableSpacing={false}
                />
            </a>
        </div>
    `
}
