prototype(Litefyr.Presentation:Block.ImageCombinations) < prototype(Neos.Fusion:Component) {
    enableText = false
    textBoxDirection = null
    textBoxText = null
    textBoxMaxWidth = null
    textBoxBreakpoint = null
    textBoxOpacity = null
    enableSpacing = null
    breakout = null
    screenHeight = null
    enableHeadline = null
    headlineFontSize = 0
    customRounded = null
    float = false
    animationMedia = null

    @private {
        hasTextbox = ${props.textBoxDirection && props.textBoxText}
        screenHeight = ${props.enableText && props.float ? null : props.screenHeight}
        textWithContentImage = afx`
            <Litefyr.Presentation:Atom.Image
                @if={!private.hasTextbox}
                {...props}
                screenHeightBreakpoint="md"
                screenHeight={private.screenHeight}
                objectFit="cover"
                breakout={null}
                rounded={false}
                animationMedia={null}
            />
            <Litefyr.Presentation:Block.ImageWithTextOverlay
                @if={private.hasTextbox}
                {...props}
                enableSpacing={false}
                screenHeightBreakpoint="md"
                screenHeight={private.screenHeight}
                objectFit="cover"
                breakout={null}
                rounded={false}
                boxDirection={props.textBoxDirection}
                boxMaxWidth={props.textBoxMaxWidth}
                boxBreakpoint={props.textBoxBreakpoint}
                backgroundOpacity={props.textBoxOpacity}
                text={props.textBoxText}
                animationMedia={null}
                clippathMargin={props.breakout == "full"}
            />
        `
        customRounded = ${Presentation.rounded(props.customRounded)}
    }

    renderer = Neos.Fusion:Case {
        @process.customRounded = Neos.Fusion:Augmenter {
            @if.set = ${private.customRounded.style}
            class = ${private.customRounded.class}
            style = ${private.customRounded.style}
        }
        enableText {
            condition = ${props.enableText}
            renderer = afx`
                <Litefyr.Presentation:Module.TextWithContent
                    {...props}
                    breakpoint="md"
                    screenHeight={private.screenHeight ? null : props.screenHeight}
                    singleContent={private.textWithContentImage}
                    stretchContent={true}
                />
            `
        }
        textBox {
            condition = ${private.hasTextbox}
            renderer = afx`
                <Litefyr.Presentation:Block.ImageWithTextOverlay
                    {...props}
                    boxDirection={props.textBoxDirection}
                    boxMaxWidth={props.textBoxMaxWidth}
                    boxBreakpoint={props.textBoxBreakpoint}
                    backgroundOpacity={props.textBoxOpacity}
                    text={props.textBoxText}
                    clippath={props.breakout == "full" ? "media" : null}
                />
            `
        }
        default {
            condition = true
            renderer = afx`
                <Litefyr.Presentation:Spacing
                    augmenter
                    width={props.enableSpacing ? props.breakout : false}
                    padding="auto"
                    tailwind="mediacontent"
                    animationMedia={props.animationMedia}
                    class={props.breakout == "full" ? Presentation.clippathClass("content", "media") : null}
                >
                    <Litefyr.Presentation:Atom.Image objectFit={props.screenHeight ? "cover" : null} {...props} />
                </Litefyr.Presentation:Spacing>
            `
        }
    }
}
