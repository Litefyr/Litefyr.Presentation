prototype(Litefyr.Presentation:Block.HeadlineAndLead) < prototype(Neos.Fusion:Component) {
    @propTypes {
        headline = ${PropTypes.string}
        label = ${PropTypes.string}
        lead = ${PropTypes.string}
        headlineLinks = ${PropTypes.string}
        isFirstElement = ${PropTypes.boolean}
        needHeadline = ${PropTypes.boolean}
        class = ${PropTypes.anyOf( PropTypes.string, PropTypes.arrayOf( PropTypes.string ) )}
    }

    @styleguide {
        props {
            headline = Sitegeist.ChitChat:Line {
                length = 30
            }
            lead = Sitegeist.ChitChat:Line {
                length = 50
            }
            label = Sitegeist.ChitChat:Line {
                length = 10
            }
        }
    }

    isFirstElement = true
    needHeadline = true
    inBackend = false
    padding = true
    leadClass = ${[this.textCenter ? 'mx-auto' : null, '-links max-w-content my-5 text-fl-base opacity-70 text-balance last:mb-0']}
    width = 'default'
    fontSize = 0
    fontSizeShift = 2

    @private.wrapperTagName = ${this.isFirstElement ? 'header' : 'div'}

    @if.hasHeadline = ${this.needHeadline ? this.headline : (this.headline || this.lead)}


    renderer = afx`
        <Litefyr.Presentation:Spacing
            padding={props.padding}
            width={props.width}
            tagName={private.wrapperTagName}
            class={Tailwind.merge('headline-alignment', props.class)}
            animationText={props.animationText}
        >
            <Litefyr.Presentation:Atom.HeadlineAndLabel
                isFirstElement={props.isFirstElement}
                inBackend={props.inBackend}
                fontSize={props.fontSize}
                fontSizeShift={props.fontSizeShift}
                content={props.headline}
                label={props.label}
            />
            <p @if={props.lead} class={props.leadClass}>
                {Presentation.typewriter(props.lead, !props.inBackend, 'text-theme-accent')}
            </p>
            {props.headlineLinks}
        </Litefyr.Presentation:Spacing>
    `
}
