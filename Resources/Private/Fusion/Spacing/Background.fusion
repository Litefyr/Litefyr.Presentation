prototype(Litefyr.Presentation:Background) < prototype(Neos.Fusion:Component) {
    @propTypes {
        theme = ${PropTypes.oneOf(['main', 'minor', 'minor2', 'gray', 'default', 'nested', 'footer', 'header', ''])}
        class = ${PropTypes.anyOf( PropTypes.string, PropTypes.arrayOf( PropTypes.any ) )}
        tailwind = ${PropTypes.anyOf( PropTypes.string, PropTypes.arrayOf( PropTypes.any ) )}
        tagName = ${PropTypes.string}
        main = ${PropTypes.boolean}
        minor = ${PropTypes.boolean}
        minor2 = ${PropTypes.boolean}
        gray  = ${PropTypes.boolean}
        nested = ${PropTypes.boolean}
        padding = ${PropTypes.boolean}
        augmenter  = ${PropTypes.boolean}
        firstHeaderCollapse  = ${PropTypes.boolean}
        clippath = ${PropTypes.oneOf(['padding', 'media', true, false])}
    }

    theme = ${this.main ? 'main' : this.minor ? 'minor' : this.minor2 ? 'minor2' : this.gray ? 'gray' : this.nested ? 'nested' : null}

    tagName = 'div'
    content = ${value}
    clippath = ${this.theme ? 'padding' : false}

    @private {
        theme = ${Array.indexOf(['main', 'minor', 'minor2', 'gray', 'default', 'nested', 'footer', 'header'], props.theme) != -1 ? props.theme : false}
        attributes = afx`
            <Neos.Fusion:DataStructure
                @ignoreProperties={['tagName', 'theme', 'main', 'minor', 'minor2', 'gray', 'nested', 'augmenter', 'content', 'padding', 'tailwind', 'firstHeaderCollapse',  'animationText', 'animationMedia', 'animationElements', 'clippath']}
                data-theme={private.theme}
                data-animation-text={props.animationText ? props.animationText : null}
                data-animation-media={props.animationMedia ? props.animationMedia : null}
                data-animation-elements={props.animationElements ? props.animationElements : null}
                {...props}
                class={Carbon.String.classNames(
                    private.theme ? '-background bg-theme-back text-theme-front' : null,
                    props.padding ? '-padding' : null,
                    props.clippath ? Presentation.clippathClass("content", props.clippath) : null,
                    props.class,
                    Carbon.Tailwind.merge(props.tailwind)
                )}
            />
        `
    }

    renderer = Neos.Fusion:Case {
        isAugmenter {
            condition = ${props.augmenter && props.tagName}
            renderer = Neos.Fusion:Augmenter {
                @apply.attributes = ${private.attributes}
                fallbackTagName = ${props.tagName}
                content = ${props.content}
            }
        }
        hasTagName {
            condition = ${props.tagName}
            renderer = Neos.Fusion:Tag {
                tagName = ${props.tagName}
                attributes = ${private.attributes}
                content = ${props.content}
            }
        }
        justTheContent {
            condition = true
            renderer = ${props.content}
        }
    }
}
